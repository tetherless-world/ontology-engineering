prefix afn:   <http://jena.apache.org/ARQ/function#>
prefix owl:   <http://www.w3.org/2002/07/owl#>
prefix rdf:   <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix xsd:   <http://www.w3.org/2001/XMLSchema#>
prefix rdfs:  <http://www.w3.org/2000/01/rdf-schema#>

##
# banner Text should not use special characters

SELECT ?error
WHERE {
  ?subject ?p ?o .
  FILTER NOT EXISTS {?o owl:deprecated "true"^^xsd:boolean} .
  FILTER (DATATYPE(?o)=xsd:string)
  FILTER regex(str(?subject), "${NAMESPACE_REGEX}")
  BIND (afn:localname (?p) AS ?prop)
  BIND ("[ÁáÇçÉéÍíÕõÖöäÄèÈμΜσΣʻa-zA-Z\\\\;'?@$%#&:/\"<*>,._+÷=)(\\[\\]{}0-9\n\t -]" AS ?reg)
  BIND (REPLACE (xsd:string (?o), ?reg, "")  AS ?bads)
  FILTER (?bads != "")
  BIND (concat ("WARN:", xsd:string(?o), " has a nonstandard character |", ?bads, "|")
	  AS ?error)
 } 

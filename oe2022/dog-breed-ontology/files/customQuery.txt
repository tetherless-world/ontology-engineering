PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX cmns-cls: <https://www.omg.org/spec/Commons/Classifiers/>
PREFIX cmns-rt: <https://www.omg.org/spec/Commons/Ratings/>
PREFIX oe2022-dogs: <https://tw.rpi.edu/ontology-engineering/oe2022/find-a-pet/>
SELECT ?label ?popularityQuantitativeScore
WHERE { 
?breed a oe2022-dogs:Breed;
	rdfs:label ?label.
?char_profile a oe2022-dogs:BreedCharacteristicProfile;
	cmns-cls:characterizes ?breed;
	oe2022-dogs:displaysBarkingLevel ?bLevel;
	oe2022-dogs:displaysStrangerFriendlinessLevel ?sfLevel;
	oe2022-dogs:displaysSheddingLevel ?sLevel;
	oe2022-dogs:displaysDroolingLevel ?dLevel;
	oe2022-dogs:displaysChildFriendlinessLevel ?cfLevel;
	oe2022-dogs:displaysPlayfullnessLevel ?pLevel;
	oe2022-dogs:displaysDogFriendlinessLevel ?dfLevel;
	oe2022-dogs:displaysTrainabilityLevel ?tLevel .

?phys_profile a oe2022-dogs:BreedPhysicalProfile;
	cmns-cls:characterizes ?breed;
	oe2022-dogs:hasMinWeight ?minW .

?popularityRating a oe2022-dogs:BreedPopularityRating;
	cmns-rt:rates ?breed;
	cmns-rt:hasRatingScore ?ratingScore.

?ratingScore cmns-rt:hasMeasureWithinScale ?popularityQuantitativeScore.

Filter not exists {?breed oe2022-dogs:causes oe2022-dogs:DogAllergy}
Filter ( ?bLevel < 0.5 && ?sfLevel >= 0.5 && ?sLevel < 0.5 && ?dLevel < 0.5 && ?cfLevel >= 0.5 && ?pLevel >= 0.5 && ?tLevel >= 0.5 && ?dfLevel >= 0.5 && ?minW < 55)

}
ORDER BY ?popularityQuantitativeScore
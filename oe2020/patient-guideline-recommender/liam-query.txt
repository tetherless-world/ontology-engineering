PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> 
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX people: <https://spec.edmcouncil.org/fibo/ontology/FND/AgentsAndPeople/People/> 
PREFIX individuals: <https://tw.rpi.edu/ontology-engineering/oe2020/patient-guideline-recommender-individuals/> 
PREFIX pgo: <https://tw.rpi.edu/ontology-engineering/oe2020/patient-guideline-recommender/>
PREFIX fiboRoles: <https://spec.edmcouncil.org/fibo/ontology/FND/Parties/Roles/>
PREFIX fiboRelations: <https://spec.edmcouncil.org/fibo/ontology/FND/Relations/Relations/>
PREFIX lcc-lr: <https://www.omg.org/spec/LCC/Languages/LanguageRepresentation/>

SELECT DISTINCT ?restriction ?filter ?match WHERE {
	pgo:Guideline-5.27 a ?restriction .
	?restriction a owl:Restriction .
	?restriction owl:onProperty fiboRelations:appliesTo .
	?restriction owl:someValuesFrom / owl:intersectionOf ?lis .
	?lis rdf:rest*/rdf:first ?filter .
	{
		?filter a owl:Restriction .
		?filter owl:onProperty ?filterProp .
		OPTIONAL {
			?filter owl:someValuesFrom ?filterValue .
			BIND(EXISTS {
				individuals:NamirXiaUser ?filterProp ?value .
				?value a ?filterValue .
			} as ?match) .
		}
	} UNION {
		FILTER NOT EXISTS {
			?filter a owl:Restriction .
			?filter owl:someValuesFrom ?x .
		}
		BIND(EXISTS {
			individuals:NamirXiaUser a ?filter .
		} as ?match) .
	}
}
